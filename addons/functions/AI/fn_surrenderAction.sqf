params ["_unit"];
#include "\x\A3A\addons\core\script_component.hpp"
FIX_LINE_NUMBERS()

if !(local _unit) exitWith {
    Error("Function miscalled with non-local unit");
	[_unit] remoteExec ["A3A_fnc_surrenderAction", _unit];
};

if (_unit getVariable ["surrendered", false]) exitWith {};
_unit setVariable ["surrendered", true, true];

if (typeOf _unit == "Fin_random_F") exitWith {};		// dogs do not surrender?

if (!alive _unit) exitWith {};							// Used to happen with ACE, seems to be fixed
if (lifeState _unit == "INCAPACITATED") exitWith {
    Info("Unit attempted to surrender while incapacitated"); // If not rare, probably a sync bug
	_unit setVariable ["surrendered", false, true];
};

private _unitSide = side _unit;
// remove unit from any associated GCs and leave only postmortem for it
if( _unit isKindOf "Man") then {
  private _surrenderGroup = objNull;
  private _grpIdx = allGroups findIf { local _x && (side _x == _unitSide) && {_x getVariable ["surrenderGroup", false]} };
  if (_grpIdx == -1) then {
  	private _grp = createGroup _unitSide;
  	_grp setVariable ["surrenderGroup", true, true];
  	_surrenderGroup = _grp;
  } else {
  	_surrenderGroup = (allGroups select _grpIdx);
  };
  _typeUnit = _unit getVariable [ "unitType", ""];
  _spawner = _unit getVariable ["spawner", false];
  _morale = _unit getVariable ["morale", 0];
  _unitPrefix = _unit getVariable ["unitPrefix", ""];
  _markerX = _unit getVariable ["markerX", ""];
  
  
  private _nameUnit = (name _unit) splitString " ";
  private _identityUnit = createHashMapFromArray [
          ["face", face _unit],
          ["speaker", _unit getVariable "A3U_PoW_speaker"],
          ["firstName", _nameUnit select 0],
          ["lastName", _nameUnit select 1]
  ];
  private _uniformUnit = uniform _unit;
  private _posUnit = getPosATL _unit;
  
  if (_unit isKindOf "CAManBase" and !(isNull (objectParent _unit))) then {
      // Otherwise vehicle seats may remain blocked
      [objectParent _unit, _unit] remoteExec ["deleteVehicleCrew", _unit];
  };
  deleteVehicle _unit;
  
  _unit = [_surrenderGroup, _typeUnit, _posUnit, [], 0, "NONE", _identityUnit] call A3A_fnc_createUnit;
  _unit allowDamage false;		// give players a couple of seconds to stop shooting
  _unit setUnitLoadout [ [], [], [], [_uniformUnit, []], [], [], "", "", [], ["","","","","",""] ];
  _unit disableAI "AUTOCOMBAT"; // ? not sure what this does. But it's used after creating a unit in fn_reinfPlayer.
  
  // move some variables back
  _unit setVariable [ "surrendered", true, true];
  _unit setVariable [ "spawner", _spawner, true];
  _unit setVariable [ "morale", _morale, true];
  _unit setVariable [ "unitPrefix", _unitPrefix, true];
  _unit setVariable [ "recreated", true, true];
  //_unit setVariable [ "markerX", _markerX, true];
  //now only postmortem can delete it
  if (!isNil "A3A_fnc_surrenderActionDebug") then {
    Info("A3A_fnc_surrenderActionDebug: recreated " + str _unit + ", " + name _unit);
  };
};

_unit setCaptive true;
_unit stop true;
_unit disableAI "MOVE";
_unit disableAI "AUTOTARGET";
_unit disableAI "TARGET";
_unit disableAI "ANIM";
_unit setSkill 0;
unassignVehicle _unit;			// stop them getting back into vehicles
[_unit] orderGetin false;
_unit setUnitPos "UP";
_unit playMoveNow "AmovPercMstpSnonWnonDnon_AmovPercMstpSsurWnonDnon";		// hands up?
_unit setVariable ["A3U_PoW_unitType", _unit getVariable "unitType", true]; // in case the original unitType is needed for something else in the future
_unit setVariable ["A3U_PoW_speaker", speaker _unit, true];
_unit setSpeaker "NoVoice";

// prevent surrendered units from spawning garrisons
if (_unit getVariable ["spawner", false]) then { _unit setVariable ["spawner", nil, true] };

// find or create suitable group for surrendered unit
private _grpIdx = allGroups findIf { local _x && (side _x == _unitSide) && {_x getVariable ["surrenderGroup", false]} };
if (_grpIdx == -1) then {
	private _grp = createGroup _unitSide;
	_grp setVariable ["surrenderGroup", true, true];
	[_unit] joinSilent _grp;
} else {
	[_unit] joinSilent (allGroups select _grpIdx);
};

// create surrender box
private _surrenderCrateType = if (_unit getVariable ["isRival", false]) then {
	A3A_faction_riv get "surrenderCrate"
} else {
	(Faction(_unitSide)) get "surrenderCrate"
};
private _boxX = _surrenderCrateType createVehicle position _unit;
_boxX allowDamage false;
clearMagazineCargoGlobal _boxX;
clearWeaponCargoGlobal _boxX;
clearItemCargoGlobal _boxX;
clearBackpackCargoGlobal _boxX;

if (_unit getVariable ["hasLaptop", false] && {!(_unit getVariable ["hasLaptopSpawned", false])}) then {
	[_unit] call SCRT_fnc_rivals_createLaptop;
	_unit setVariable ["hasLaptopSpawned", true, true]; //not sure if it should be broadcasted
};

// move all unit's equipment except uniform into the surrender crate
private _loadout = getUnitLoadout _unit;
for "_i" from 0 to 2 do {
	if !(_loadout select _i isEqualTo []) then {
		_boxX addWeaponWithAttachmentsCargoGlobal [_loadout select _i, 1];
	};
};
{_boxX addMagazineCargoGlobal [_x,1]} forEach (magazines _unit);
{_boxX addItemCargoGlobal [_x,1]} forEach (assignedItems _unit);
{_boxX addItemCargoGlobal [_x,1]} forEach (items _unit);
{_boxX addItemCargoGlobal [_x,1]} forEach [vest _unit, headgear _unit, goggles _unit];
private _backpack = backpack _unit;
if (_backpack != "") then {
	// because backpacks are often subclasses containing items
	_backpack = _backpack call A3A_fnc_basicBackpack;
	_boxX addBackpackCargoGlobal [_backpack, 1];
};
_unit setUnitLoadout [ [], [], [], [uniform _unit, []], [], [], "", "", [], ["","","","","",""] ];

//find and properly dispose dropped weapons
{
	_boxX addWeaponWithAttachmentsCargoGlobal [(weaponsItemsCargo _x) select 0, 1];
	deleteVehicle _x;
} forEach nearestObjects [_unit,["WeaponHolderSimulated"],5,true];

if (_unitSide == Occupants) then {
	[-2, 0, getPos _unit] remoteExec ["A3A_fnc_citySupportChange", 2];
} else {
	[0, 1, getPos _unit] remoteExec ["A3A_fnc_citySupportChange", 2];
};

private _markerX = _unit getVariable "markerX";
if (!isNil "_markerX") then { [_markerX, _unitSide] remoteExec ["A3A_fnc_zoneCheck", 2] };


// timed cleanup functions
[_unit] spawn A3A_fnc_postmortem;
[_boxX] spawn A3A_fnc_postmortem;

sleep 3;				// Also protects against box kills
_unit allowDamage true;
private _handlerDamage = _unit addEventHandler ["HandleDamage", {
	// If unit gets injured after the delay, run away
	params ["_unit","_part","_damage"];
	if (_damage < 0.2) exitWith {};
	[_unit, "remove"] remoteExec ["A3A_fnc_flagaction", [teamPlayer, civilian], _unit];
	[_unit, side group _unit] spawn A3A_fnc_fleeToSide;
	_unit removeEventHandler ["HandleDamage", _thisEventHandler];
	nil;
}];

_unit setVariable ["A3U_PoW_EH_HandleDamage", _handlerDamage, true];

if (_unit getVariable ["isRival", false]) then {
	[_unit,"captureRivals"] remoteExec ["A3A_fnc_flagaction",[teamPlayer,civilian],_unit];
} else {
	[_unit,"captureX"] remoteExec ["A3A_fnc_flagaction",[teamPlayer,civilian],_unit];
};
