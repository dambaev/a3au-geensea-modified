// Repairs a radio tower.
// Parameter should be present in antennasDead array
#include "\x\A3A\addons\core\script_component.hpp"
FIX_LINE_NUMBERS()
if (!isServer) exitWith { Error("Server-only function miscalled") };

params ["_antenna"];

if !(_antenna in antennasDead) exitWith { Error("Attempted to rebuild invalid radio tower") };
Info_1("Repairing Antenna %1", str _antenna);

antennasDead = antennasDead - [_antenna]; publicVariable "antennasDead";
[_antenna] call A3A_fnc_repairRuinedBuilding;
antennas pushBack _antenna; publicVariable "antennas";

{if ([antennas,_x] call BIS_fnc_nearestPosition == _antenna) then {[_x,true] spawn A3A_fnc_blackout}} forEach citiesX;

private _mrkFinal = createMarker [format ["Ant%1", mapGridPosition _antenna], getPos _antenna];
_mrkFinal setMarkerShape "ICON";
_mrkFinal setMarkerType "loc_Transmitter";
_mrkFinal setMarkerColor "ColorBlack";
_mrkFinal setMarkerText (localize "STR_radiotower");
mrkAntennas pushBack _mrkFinal;
publicVariable "mrkAntennas";

_antenna addEventHandler ["Killed", {
	params ["_antenna"];
	_antenna removeAllEventHandlers "Killed";
	{if ([antennas,_x] call BIS_fnc_nearestPosition == _antenna) then {[_x,false] spawn A3A_fnc_blackout}} forEach citiesX;
	_mrk = [mrkAntennas, _antenna] call BIS_fnc_nearestPosition;
	mrkAntennas deleteAt(mrkAntennas find _mrk);
	antennas deleteAt(antennas find _antenna);
	deleteMarker _mrk;
	antennasDead pushBack _antenna;
	publicVariable "antennas";
	publicVariable "antennasDead";
	publicVariable "mrkAntennas";
	["TaskSucceeded",["", localize "STR_notifiers_radiotower_destroyed"]] remoteExec ["BIS_fnc_showNotification",teamPlayer];
	["TaskFailed",["", localize "STR_notifiers_radiotower_destroyed"]] remoteExec ["BIS_fnc_showNotification",Occupants];
  private _killer = _this select 1;
  if( (!isNil { _killer}) && ( !isNull(_killer)) ) then {
    if( isPlayer _killer && side _killer == independent) then {
      [ 5000] remoteExec [ "A3A_fnc_resourcesPlayer", _killer];
    };
    if( (!isNil {theBoss}) && (!isNull theBoss)) then {
      [ 1000] remoteExec [ "A3A_fnc_resourcesPlayer", theBoss];
    };
    [ 0, 5000] call A3A_fnc_resourcesFIA;
    private _any_occ_group = allGroups select {
           (count ( (units _x) select {
                ([ _x ] call A3A_fnc_canFight)
             && ( (_x getVariable ["A3A_canCallSupportAt", -1]) <= time)
           } ) > 0)
        && (side _x == Occupants)
      };
    if( count _any_occ_group > 0) then {
      [ _any_occ_group select 0, _killer ] spawn A3A_fnc_callForSupport;
    };
  };
}];

